$( ->
  initialize_masonry()
  initialize_infinitescroll()
)

window.destroy_infinitescroll = ->
  $container = $('#container')
  $container.infinitescroll('destroy')
  $container.data('infinitescroll', null)

window.initialize_infinitescroll = ->
  $container = $('#container')
  $container.infinitescroll(infinitescroll_options(), (newElements) ->
    $(newElements).addClass('hidden')
    $(newElements).css({ opacity: 0 }).imagesLoaded( ->
      @.removeClass('hidden')
      resize_images()

      if can_scroll()
        $container.masonry('appended', @, true)
      else
        $container.infinitescroll('retrieve')
        $container.masonry('reload')

      @.animate({ opacity: 1 })
    )
  )
  $container.infinitescroll('bind')

window.initialize_masonry = ($container) ->
  $container = $('#container') unless $container
  $container.css({ opacity: 0 }).imagesLoaded( ->
    @.removeClass('hidden')
    @.masonry({
      isFitWidth: true,
      itemSelector: '.box',
      isAnimated: false
    })
    @.animate({ opacity: 1 })
    @.infinitescroll('retrieve') unless can_scroll()
  )

window.resize_images = ($container) ->
  $container = $('#container') unless $container
  $container.find('.image_box img').each( ->
    width = <%= Settings.thumb_width %>
    height = Math.floor($(@).height() * (width / $(@).width() * 1.0))
    if is_available_image_size(width, height)
      $(@).width(width)
      $(@).height(height)
    else
      $(@).closest('.box').remove()
  )

is_available_image_size = (width, height) ->
  horizontal_aspect = <%= Settings.allowed_image_aspect[0] %> * 1.0
  vertical_aspect = <%= Settings.allowed_image_aspect[1] %> * 1.0
  aspect = (width * 1.0) / (height * 1.0)
  width > <%= Settings.minimum_image_width %> &&
    height > <%= Settings.minimum_image_height %> &&
    aspect <= (horizontal_aspect / vertical_aspect) &&
    aspect >= (vertical_aspect / horizontal_aspect)

infinitescroll_options = ->
  additional_options = {}
  additional_options = infinitescroll_options_for_dialog() if is_infinitescroll_on_dialog()
  $.extend({
    navSelector: '#page-nav',     # ページのナビゲーションを選択
    nextSelector: '#page-nav a:first',  # 次ページへのリンク
    itemSelector: '.box',         # 持ってくる要素のclass
    loading: {
      finishedMsg: 'すべてのクリップを読み込みました',
      msgText: 'クリップを読み込み中...',
      img: '<%= image_path 'loader.gif' %>' # ローディング画像のパス
    },
    state: {
      isDestroyed: false,
      isDone: false
    },
    errorCallback: ->
      $('#done').removeClass('hidden')
    # URLをうまくパースできない場合があるので独自にパース
    pathParse: (path, currentPage) ->
      path.match(/^(.*?page=)2(\/.*|$)/).slice(1)
  }, additional_options)

infinitescroll_options_for_dialog = ->
  {
    behavior: 'local',
    binder: $('#container')
  }

window.can_scroll = ->
  # ダイアログが開かれている場合はそちらを優先
  if is_visible_dialog()
    $container = $('#container')
    $container.height() <= $container.get(0).scrollHeight
  else
    # documentだとinfiniscroll後、期待するコンテンツの高さが取得できないので'html'にする
    $(window).height() <= $('html').height()

is_visible_dialog = ->
  $('.ui-dialog').is(':visible')

is_infinitescroll_on_dialog = ->
  $('.dialog').find('#container').length > 0
